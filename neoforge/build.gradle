buildscript {
    repositories {
        maven {
            name 'NeoForge Maven'
            url 'https://maven.neoforged.net/releases'
        }
        maven {
            name 'SpongePowered'
            url 'https://repo.spongepowered.org/maven'
        }

        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'net.neoforged.gradle', name: 'net.neoforged.gradle.gradle.plugin', version: '[6.0.18,6.2)', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'net.neoforged.gradle'
apply plugin: 'org.spongepowered.mixin'

// Load version configs
def vProps = new Properties()
file("../config.properties").withInputStream { vProps.load(it) }

archivesBaseName = vProps.archives_base_name
version = "v${vProps.mod_version}+neoforge-${vProps.minecraft_version}"
group = vProps.maven_group

java.toolchain.languageVersion = JavaLanguageVersion.of(vProps.java_version as Integer)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'official', version: vProps.minecraft_version

    runs {
        client {
            sourceSets.main.java.srcDirs += ['../src/neoforge/java']
            sourceSets.main.java.srcDirs += ['../src/main/java']
            sourceSets.main.resources.srcDirs += ['../src/neoforge/resources']
            sourceSets.main.resources.srcDirs += ['../src/main/resources']
        }
    }
}

mixin {
    add sourceSets.main, 'disable_compliance_notification.refmap.json'
}

dependencies {
    // NOTE: `vProps.minecraft_version.substring(2)` when 20.2 is released
    minecraft "net.neoforged:forge:${vProps.minecraft_version}-${vProps.neoforge_version}"

    annotationProcessor "org.spongepowered:mixin:${vProps.mixin_processor_version}:processor"

    api fg.deobf("me.shedaniel.cloth:cloth-config-forge:${vProps.cloth_config_version}")
}

repositories {
    maven { url = 'https://maven.neoforged.net/releases' }
    jcenter()
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = 'https://maven.shedaniel.me/' }
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", vProps.minecraft_version

    filesMatching("META-INF/mods.toml") {
        expand "version": vProps.mod_version,
                "minecraft_version": vProps.minecraft_version,
                "neoforge_major_version": vProps.neoforge_version.split('\\.')[0],
                "cloth_config_major_version": vProps.cloth_config_version.split('\\.')[0]
    }

    filesMatching("disable_compliance_notification.mixins.json") {
        expand "java_version": vProps.java_version
    }

    filesMatching("pack.mcmeta") {
        expand "pack_format": vProps.pack_format
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "DisableComplianceNotification",
                "Specification-Vendor"    : "MPThLee",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : "Disable Compliance Notification",
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "MPThLee",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "disable_compliance_notification.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    options.release = vProps.java_version as Integer
}
